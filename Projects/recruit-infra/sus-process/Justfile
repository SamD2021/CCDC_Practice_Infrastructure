image_name := env("BUILD_IMAGE_NAME", "quay.io/samueldasilva/recruit-task")
image_tag := env("BUILD_IMAGE_TAG", "easy")
base_dir := env("BUILD_BASE_DIR", ".")
filesystem := env("BUILD_FILESYSTEM", "ext4")

build-containerfile *ARGS:
    sudo podman build --security-opt label=disable --network=host -t "{{image_name}}:{{image_tag}}" -f Containerfile.{{image_tag}} .

push-containerfile $image_name=image_name $image_tag=image_tag:
    sudo podman push "${image_name}:${image_tag}"

bootc *ARGS:
    sudo podman run \
        --rm --privileged --pid=host \
        -it \
        -v /sys/fs/selinux:/sys/fs/selinux \
        -v /etc/containers:/etc/containers:Z \
        -v /var/lib/containers:/var/lib/containers:Z \
        -v /dev:/dev \
        -v "{{base_dir}}:/data" \
        --security-opt label=type:unconfined_t \
        "{{image_name}}:{{image_tag}}" bootc {{ARGS}}

generate-bootable-image $base_dir=base_dir $filesystem=filesystem:
    #!/usr/bin/env bash
    if [ ! -e "${base_dir}/bootable.img" ] ; then
        fallocate -l 20G "${base_dir}/bootable.img"
    fi
    just bootc install to-disk --composefs-native --via-loopback /data/bootable.img --filesystem "${filesystem}" --wipe --bootloader systemd
